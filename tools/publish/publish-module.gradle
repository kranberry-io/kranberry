apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'org.jetbrains.dokka'

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        // For Android libraries
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        // For pure Kotlin libraries, in case you have them
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    archiveClassifier.set('javadoc')
    from dokkaJavadoc.outputDirectory
}

artifacts {
    archives androidSourcesJar
    archives javadocJar
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                artifact androidSourcesJar
                artifact javadocJar

                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = 'Library to help on the end2end tests implementation on Android'
                    url = 'https://github.com/kranberry-io/kranberry'
                    licenses {
                        license {
                            name = 'Kranberry License'
                            url = 'https://github.com/kranberry-io/kranberry'
                        }
                    }
                    developers {
                        developer {
                            id = 'knludi'
                            name = 'Ana Ludmila'
                            email = 'knludi@gmail.com'
                        }
                        developer {
                            id = 'angelica-oliv'
                            name = 'Angelica Oliveira'
                            email = 'angelica.liv@gmail.com'
                        }
                    }

                    scm {
                        connection = 'scm:git:github.com/kranberry-io/kranberry.git'
                        developerConnection = 'scm:git:ssh://github.com/kranberry-io/kranberry.git'
                        url = 'https://github.com/kranberry-io/kranberry/tree/master'
                    }
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
            rootProject.ext["signing.keyId"],
            rootProject.ext["signing.key"],
            rootProject.ext["signing.password"],
    )
    sign publishing.publications
}